apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

configurations.all {
    // check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    compile group: 'org.slf4j', name: 'slf4j-jdk14', version: '1.7.13'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
    compile group: 'com.bortbort.arduino', name: 'filo-firmata', version: '0.1.9'

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'com.jayway.awaitility', name: 'awaitility', version: '1.7.0'
}

group 'com.bortbort.arduino'
archivesBaseName = "model-core"
version '0.1.0'
sourceCompatibility = 1.8



task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}


artifacts {
    archives javadocJar, sourcesJar
}


signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom {
                project {
                    name 'ArduinoModel-Core'
                    packaging 'jar'
                    description 'ArduinoModel-Core is the core library for supporting a pure Java representation of a hardware Arduino project board.'
                    url 'https://github.com/reapzor/ArduinoModel-Core'

                    scm {
                        url 'scm:git@github.com:reapzor/ArduinoModel-Core.git'
                        connection 'scm:git@github.com:reapzor/ArduinoModel-Core.git'
                        developerConnection 'scm:git@github.com:reapzor/ArduinoModel-Core.git'
                    }

                    licenses {
                        license {
                            name 'The MIT License (MIT)'
                            url 'https://github.com/reapzor/ArduinoModel-Core/blob/master/LICENSE'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'reapzor'
                            name 'Chuck Benson'
                        }
                    }
                }
            }.writeTo("pom.xml")
        }
    }
}
